Option Explicit

Sub StockDataAnalysis()
    Dim ws As Worksheet
    
    For Each ws In ThisWorkbook.Worksheets
        Call StockData(ws)
    Next ws
End Sub
Sub StockData(ws As Worksheet)
    ws.Activate
    ws.Range("I1").Value = "Ticker"
    ws.Range("J1").Value = "Yearly Change"
    ws.Range("K1").Value = "Percent Change"
    ws.Range("L1").Value = "Total Stock Volume"
    Const FIRST_DATA_ROW As Integer = 2
    Const OUT_TICKER_COL As Integer = 9
    Const OUT_YEARLY_CHANGE_COL As Integer = 10
    Const OUT_PERCENT_CHANGE_COL As Integer = 11
    Const OUT_TOTAL_VOLUME_COL As Integer = 12
        
    Dim lastRow As Long
    Dim tickerSymbol As String
    Dim yearlyChange As Double
    Dim changeFrac As Double
    Dim totalVolume As Double
    Dim openPrice As Double
    Dim closePrice As Double
    Dim inputRow As Long
    Dim outputRow As Long
    Dim greatestIncrease As Double
    Dim greatestDecrease As Double
    Dim greatestVolume As Double
    Dim tickerGreatestIncrease As String
    Dim tickerGreatestDecrease As String
    Dim tickerGreatestVolume As String
    

    lastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
    greatestIncrease = -999999999
    greatestDecrease = 999999999
    greatestVolume = 0
    totalVolume = 0
    outputRow = FIRST_DATA_ROW
    openPrice = ws.Cells(FIRST_DATA_ROW, 3).Value
For inputRow = FIRST_DATA_ROW To lastRow
    tickerSymbol = ws.Cells(inputRow, 1).Value
    totalVolume = totalVolume + ws.Cells(inputRow, 7).Value
    If ws.Cells(inputRow + 1, 1).Value <> tickerSymbol Then
       
        closePrice = ws.Cells(inputRow, 6).Value
        yearlyChange = closePrice - openPrice
        If openPrice <> 0 Then
            changeFrac = (yearlyChange / openPrice)
      
        End If
             
        If yearlyChange >= 0 Then
            ws.Cells(outputRow, OUT_YEARLY_CHANGE_COL).Interior.Color = vbGreen
        Else
            ws.Cells(outputRow, OUT_YEARLY_CHANGE_COL).Interior.Color = vbRed
        End If
        
        If changeFrac > greatestIncrease Then
            greatestIncrease = changeFrac
            tickerGreatestIncrease = tickerSymbol
        ElseIf changeFrac < greatestDecrease Then
            greatestDecrease = changeFrac
            tickerGreatestDecrease = tickerSymbol
        End If
        If totalVolume > greatestVolume Then
        greatestVolume = totalVolume
        tickerGreatestVolume = tickerSymbol
        End If
        
        'write annual summary
        ws.Cells(outputRow, OUT_TICKER_COL).Value = tickerSymbol
        ws.Cells(outputRow, OUT_YEARLY_CHANGE_COL).Value = yearlyChange
        ws.Cells(outputRow, OUT_PERCENT_CHANGE_COL).Value = FormatPercent(changeFrac)
        ws.Cells(outputRow, OUT_TOTAL_VOLUME_COL).Value = totalVolume
        
        'prepare for next stock
        totalVolume = 0
        outputRow = outputRow + 1
        openPrice = ws.Cells(inputRow + 1, 3).Value
        
    End If
Next inputRow

ws.Range("P1").Value = "Ticker"
ws.Range("Q1").Value = "Value"
ws.Range("O2").Value = "Greatest % Increase"
ws.Range("O3").Value = "Greatest % Decrease"
ws.Range("O4").Value = "Greatest Total Volume"

ws.Range("P2").Value = tickerGreatestIncrease
ws.Range("Q2").Value = greatestIncrease
ws.Range("P3").Value = tickerGreatestDecrease
ws.Range("Q3").Value = greatestDecrease
ws.Range("P4").Value = tickerGreatestVolume
ws.Range("Q4").Value = greatestVolume
        
End Sub
